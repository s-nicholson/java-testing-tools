plugins {
    id 'java'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.15.0'
    id "io.freefair.lombok" version "8.6"
}

group = 'com.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'

    implementation 'com.h2database:h2:2.2.220'
    implementation 'org.postgresql:postgresql:42.6.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.skyscreamer:jsonassert:1.5.1'
    testImplementation 'org.assertj:assertj-core:3.24.2'

    testImplementation 'com.github.tomakehurst:wiremock-jre8-standalone:2.35.0'

    testImplementation 'io.cucumber:cucumber-java:7.13.0'
    testImplementation 'io.cucumber:cucumber-junit:7.13.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.13.0'

    testImplementation 'org.testcontainers:postgresql:1.18.3'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

test {
    useJUnitPlatform()

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

check.dependsOn 'jacocoTestCoverageVerification'

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

pitest {
    junit5PluginVersion = '1.2.1'
    mutationThreshold = 90
    mutators = ['ALL']
    targetClasses = ['com.example.testing.pitest.*']
}

check.dependsOn 'pitest'